apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "me.wcy.weather"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 220
        versionName "2.2.0"

        buildConfigField 'String', 'FIR_KEY', getLocalValue("FIR_KEY", true)
        buildConfigField 'String', 'BMOB_KEY', getLocalValue("BMOB_KEY", true)
        buildConfigField 'String', 'HE_KEY', getLocalValue("HE_KEY", true)
        buildConfigField 'String', 'BD_TTS_API_KEY', getLocalValue("BD_TTS_API_KEY", true)
        buildConfigField 'String', 'BD_TTS_SECRET_KEY', getLocalValue("BD_TTS_SECRET_KEY", true)

        manifestPlaceholders = [AMAP_KEY  : getLocalValue("AMAP_KEY"),
                                BUG_HD_KEY: getLocalValue("BUG_HD_KEY")]

        ndk {
            abiFilters "armeabi", "armeabi-v7a"
        }
    }

    signingConfigs {
        release {
            storeFile file("wangchenyan.keystore")
            storePassword getLocalValue("STORE_PASSWORD")
            keyAlias getLocalValue("KEY_ALIAS")
            keyPassword getLocalValue("KEY_PASSWORD")
        }
        debug {
            storeFile file("debug.keystore")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

def getLocalValue(String key) {
    return getLocalValue(key, false)
}

def getLocalValue(String key, boolean quot) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def value = properties.containsKey(key) ? properties.get(key) : ""
    if (quot) {
        value = "\"" + value + "\"";
    }
    return value
}

ext {
    supportVersion = "25.3.1"
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:${supportVersion}"
    compile "com.android.support:design:${supportVersion}"
    compile "com.android.support:cardview-v7:${supportVersion}"
    compile "io.reactivex.rxjava2:rxjava:2.1.3"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'com.getbase:floatingactionbutton:1.10.1'
    compile 'im.fir:fir-sdk:1.3.7'
    // 删除了okio,gson的bmob包(重复)
    compile(name: 'bmob-sdk-3.5.7', ext: 'aar')
}
